@echo off
echo Starting MySQL container
docker run --name opa-mysql -e MYSQL_ROOT_PASSWORD=Passw0rd --health-cmd="mysqladmin ping --silent" -d mysql:5.7.21

echo Starting Weblogic container
docker run --name opa-weblogic -e ADMIN_PASSWORD=Passw0rd -p 7001:7001 --link opa-mysql:opa-mysql --health-cmd="curl -f http://localhost:7001/console || exit 1" -d container-registry.oracle.com/middleware/weblogic:12.2.1.3

:loop1
for /f %%a in ('docker inspect --format="{{.State.Health.Status}}" opa-mysql') do set "isMySqlRunning=%%a"
if NOT "%isMySqlRunning%" == "healthy" (
  echo Waiting for MySQL container...
  timeout /t 5 /nobreak
  goto loop1
)

:loop2
for /f %%a in ('docker inspect --format="{{.State.Health.Status}}" opa-weblogic') do set "isWeblogicRunning=%%a"
if NOT "%isWeblogicRunning%" == "healthy" (
  echo Waiting for Weblogic container...
  timeout /t 5 /nobreak
  goto loop2
)

echo All containers running

echo Installing OPA
docker cp opa opa-weblogic:/u01/oracle/opa
docker exec --user root opa-weblogic chown oracle:oracle -R /u01/oracle/opa
docker exec opa-weblogic /u01/oracle/opa/bin/install.sh install -non-secure-cookie=true -name=dev -dbconn=opa-mysql:3306 -dbuser=root -dbpass=Passw0rd -hubpass=Passw0rd -key=12345678 -wladmin=AdminServer -wladminurl=t3://localhost:7001 -target=AdminServer -wldomain=/u01/oracle/user_projects/domains/base_domain -wlstdir=/u01/oracle/wlserver/common/bin

echo Your hub is now ready at http://localhost:7001/dev/opa-hub
echo Username: admin
echo The autogenerated admin password can be found about 10 lines above.

REM docker exec -i -t opa-weblogic /bin/bash